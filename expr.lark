%import common.INT -> INT
%import common.CNAME -> ID
%import common.WS

%ignore WS

?expr1: "if" expr1 "then" expr1 "else" expr1 -> if
      | expr2

?expr2: expr2 "||" expr4 -> or
      | expr3

?expr3: expr3 "&&" expr4 -> and
      | expr4

?expr4: "!" expr4 -> not
      | expr5

?expr5: expr5 "==" expr6 -> eq
      | expr5 "<" expr6 -> lt
      | expr6  

?expr6: expr6 "+" term  -> plus
      | expr6 "-" term -> minus
      | term

?term: term "*" factor -> times
     | term "/" factor -> divide
     | factor

?factor: "-" factor  -> neg
       | atom

?atom: ID  -> id
     | INT -> int
     | atom "(" arg_list ")" -> app
     | "(" expr1 ")"
     | "let" ID "=" expr1 "in" expr1 "end" -> let
     | "letfun" ID "(" param_list ")" "=" expr1 "in" expr1 "end" -> letfun

param_list: [ID ("," ID)*]
arg_list: [expr1 ("," expr1)*]